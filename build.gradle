plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.1"
}

apply plugin: 'maven'

def BUILD_NUMBER = System.getenv("BUILD_NUMBER") ?: "0"
def MAVEN_REPO = System.getenv("MAVEN_REPO") ?: ""
def MAVEN_USERNAME = System.getenv("MAVEN_USERNAME") ?: ""
def MAVEN_PASSWORD = System.getenv("MAVEN_PASSWORD") ?: ""

version = "3.9." + BUILD_NUMBER
sourceCompatibility = 1.7
targetCompatibility = 1.7
group = "net.blay09.mods"
archivesBaseName = "eirairc"

minecraft {
    version = "1.8-11.14.3.1512"
    runDir = "run"
    
    mappings = "snapshot_20141130"
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    maven {
        name = "dynmap"
        url = "http://repo.mikeprimm.com/"
    }
    maven {
        name = "eiranet"
        url ="http://repo.blay09.net"
    }
}

dependencies {
    deobfCompile 'net.blay09.mods:eiramoticons:2.1.40'
    compile 'us.dynmap:DynmapCoreAPI:2.1'
    compile 'mnm.mods:MnM-Utils:0.3.1-SNAPSHOT-14:dev'
    compile 'mnm.mods:TabbyChat:2.0.0-beta3.1-SNAPSHOT-14:dev'
}

jar {
    from sourceSets.api.output
    appendix "mc" + minecraft.version
}

sourceJar {
    appendix "mc" + minecraft.version
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    classifier "api"
    appendix "mc" + minecraft.version
}

artifacts {
    archives apiJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: MAVEN_REPO) {
                authentication(userName: MAVEN_USERNAME, password: MAVEN_PASSWORD)
            }
            
            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
            }

            pom.withXml {
                asNode().dependencies.dependency.findAll {
                    it.scope.text() == "compile"
                }.each {
                    if(it.optional)
                        it.optional.value = 'true'
                    else
                        it.appendNode('optional', 'true');
                }
            }
        }
    }
}